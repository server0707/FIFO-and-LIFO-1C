
Процедура ОбработкаПроведения(Отказ, Режим)
	МетодСписания = Константы.ТипОчереди.Получить();
	
	Если МетодСписания.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не указан метод списания! Проведение не возможно";
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Средняя = МетодСписания = Перечисления.ТипОчереди.Средняя;

	Движения.ОстаткиНоменклатуры.Записывать = Истина;
	Движения.Продажи.Записывать=Истина;
	
	Движения.ОстаткиНоменклатуры.Записать();
	Движения.Продажи.Записать();
	
		//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
	// Данный фрагмент построен конструктором.
	// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура КАК Номенклатура,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Количество) КАК Количество,
		|	СУММА(РасходнаяНакладнаяСписокНоменклатуры.Сумма) КАК Сумма
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	Документ.РасходнаяНакладная.СписокНоменклатуры КАК РасходнаяНакладнаяСписокНоменклатуры
		|ГДЕ
		|	РасходнаяНакладнаяСписокНоменклатуры.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РасходнаяНакладнаяСписокНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО 
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ.Номенклатура КАК Номенклатура,
		|	ВТ.Количество КАК Количество,
		|	ВТ.Сумма КАК Сумма,
		|	ОстаткиНоменклатурыОстатки.Партия КАК Партия,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
		|	ЕСТЬNULL(ОстаткиНоменклатурыОстатки.СуммаОстаток, 0) КАК СуммаОстаток
		|ИЗ
		|	ВТ КАК ВТ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиНоменклатуры.Остатки(
		|				&МоментВремени,
		|				Номенклатура В
		|					(ВЫБРАТЬ
		|						ВТ.Номенклатура КАК Номенклатура
		|					ИЗ
		|						ВТ КАК ВТ)) КАК ОстаткиНоменклатурыОстатки
		|		ПО ВТ.Номенклатура = ОстаткиНоменклатурыОстатки.Номенклатура
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОстаткиНоменклатурыОстатки.Партия.МоментВремени ВОЗР
		|ИТОГИ
		|	МАКСИМУМ(Количество),
		|	МАКСИМУМ(Сумма),
		|	СУММА(КоличествоОстаток),
		|	СУММА(СуммаОстаток)
		|ПО
		|	Номенклатура";
	
	Запрос.УстановитьПараметр("МоментВремени", МоментВремени());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Если МетодСписания = Перечисления.ТипОчереди.LIFO Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВОЗР", "УБЫВ"); // если лифо - сортируем по УБЫВанию
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНоменклатура = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
		Если ВыборкаНоменклатура.КоличествоОстаток < ВыборкаНоменклатура.Количество Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает " + Строка(ВыборкаНоменклатура.Количество - ВыборкаНоменклатура.КоличествоОстаток) + " единиц номенклатуры " + ВыборкаНоменклатура.Номенклатура + ". Проведение невозможно.";
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		ОсталосьСписать = ВыборкаНоменклатура.Количество;
		
		ВыборкаДетЗаписи = ВыборкаНоменклатура.Выбрать(); 
	
		Пока ВыборкаДетЗаписи.Следующий() И ОсталосьСписать <> 0 Цикл
			
			КСписанию = Мин(ОсталосьСписать, ВыборкаДетЗаписи.КоличествоОстаток);
			
			Если Средняя Тогда
				СуммаСписания = ?(ВыборкаНоменклатура.КоличествоОстаток = КСписанию,
				ВыборкаНоменклатура.СуммаОстаток,
				ВыборкаНоменклатура.СуммаОстаток/ВыборкаНоменклатура.КоличествоОстаток * КСписанию);
			Иначе
				СуммаСписания = ?(ВыборкаДетЗаписи.КоличествоОстаток = КСписанию,
				ВыборкаДетЗаписи.СуммаОстаток,
				ВыборкаДетЗаписи.СуммаОстаток/ВыборкаДетЗаписи.КоличествоОстаток * КСписанию);
			КонецЕсли;
			
			НовоеДвижение = Движения.ОстаткиНоменклатуры.ДобавитьРасход();
			НовоеДвижение.Период = Дата;
			НовоеДвижение.Регистратор = Ссылка;
			НовоеДвижение.Количество = КСписанию;
			НовоеДвижение.Номенклатура = ВыборкаДетЗаписи.Номенклатура;
			Если НЕ Средняя Тогда
				НовоеДвижение.Партия = ВыборкаДетЗаписи.Партия;
			КонецЕсли;
			НовоеДвижение.Сумма = СуммаСписания;
			
			НовоеДвижение = Движения.Продажи.Добавить();
			НовоеДвижение.Активность = Истина;
			НовоеДвижение.Период = Дата;
			НовоеДвижение.Регистратор = Ссылка;
			НовоеДвижение.Номенклатура = ВыборкаДетЗаписи.Номенклатура;
			НовоеДвижение.Сумма = ВыборкаДетЗаписи.Сумма;			
			НовоеДвижение.Количество = КСписанию;
			НовоеДвижение.Себестоимость = СуммаСписания;
			ОсталосьСписать = ОсталосьСписать - КСписанию;
		КонецЦикла;
	КонецЦикла;                                         

КонецПроцедуры
